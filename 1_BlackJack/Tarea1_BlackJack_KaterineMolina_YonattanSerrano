'************************************************************************************
'*****************           SIMULACION- II SEMESTRE 2018           *****************
'*****************                 TAREA 3 Poker                    *****************
'*****************        KATERINE MOLINA SANCHEZ 2013109225        *****************
'*****************        YONATTAN SERRANO TORRES 2014005692        *****************
'************************************************************************************


'************************************************************************************************

' Running de program: 
' You just have to run this program and select the file: 90_Test_Uniforme_01.txt

'************************************************************************************************

' Results: 

' OFList  	EFList  
' 311     	302.4   	
' 499     	504     	
' 107     	108     	
' 72      	72      	
' 6       	9       	
' 5       	4.5     	
' 0       	0.1     	
' s0: 1.4589947089947104



'************************************************************************************************

' H0: YES, it's a distribution f(x) with its parameters
' H1: NO, It isn't a distribution f(x) with its parameters


' To calculate the rejection point we will use an alpha value of 0.05 with 
' 7-1 = 6 degrees of freedom.

' \/2
' /\ (0.05,6) = 12.5916 (This value is from the Ji square table)

' As the value of 1.4589947089947104 <= 12.5916, H0 is accepted and therefore it is assumed that 
' the numbers generated correspond to a uniform distribution.


'************************************************************************************************


CLEAROUTPUT


' Global variables -------------------------------------------------
COPY (0 0 0 0 0 0 0 ) OFList  ' Observed Frecuencies List
COPY (0 0 0 0 0 0 0 ) EFList  ' Expected Frecuencies List
COPY (0.3024 0.5040 0.1080 0.0720 0.0090 0.0045 0.0001) EPList 'Expected Probabilities List


NEWCMD INCREASE_COUNTER key list
   TAKE list key num
   num = num + 1 ' INCREASE
   PUT  num key list
END


NEWCMD FILTER_NUMBER numberList fo
' 5 numbers repeated
   MULTIPLES numberList =5 result
   IF result=1
      INCREASE_COUNTER 7 fo
   ELSE
      MULTIPLES numberList =4 result
      IF result = 1
         INCREASE_COUNTER 6 fo
      ELSE
         MULTIPLES numberList =3 result
         IF result = 1
            MULTIPLES numberList =2 result
            IF result = 1
               INCREASE_COUNTER 5 fo
            ELSE
               INCREASE_COUNTER 4 fo
            END
         ELSE
            MULTIPLES numberList =2 result
            IF result = 2
               INCREASE_COUNTER 3 fo
            ELSEIF result = 1
               INCREASE_COUNTER 2 fo
            ELSE
               MULTIPLES numberList =1 result
               IF result =5
                  INCREASE_COUNTER 1 fo
               END
            END
         END
      END
   END
END
'***** PROFESSOR CODE
NEWCMD EXPLOTE num list
   list = ()
   t = num * 100000
   INTEGER truncate t t
   numCycles = 5
   REPEAT numCycles
      rem = t % 10
      quo = t / 10
      INTEGER truncate quo quo
      
      t = quo
      SCORE rem list
   END
END


NEWCMD GET_OBSERVED_FRECUENCIES list OFList
   FOREACH num list
      EXPLOTE num numList
      FILTER_NUMBER numList OFlist
   END
END

NEWCMD GET_EXPECTED_FRECUENCIES sampleSize  EFList EPList
   index = 1
   FOREACH probability EPList
      expectedFrecuency = probability * sampleSize
      PUT expectedFrecuency index EFList
      index = index +1
   END
END


'MAIN PROGRAM -----------------------------------------------------

READ file "" fileList

GET_OBSERVED_FRECUENCIES  fileList OFList
SIZE fileList fileSize
GET_EXPECTED_FRECUENCIES fileSize EFList EPList


'Calculate chiSquare Num
CHISQUARE OFList EFList s0
'--Print tables and result------------------
PRINT table OFList%8.0F EFList%8.4F
PRINT s0
