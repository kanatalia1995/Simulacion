'************************************************************************************
'*****************           SIMULACION- II SEMESTRE 2018           *****************
'*****************                 TAREA1 BLACKJACK                 *****************
'*****************        KATERINE MOLINA SANCHEZ 2013109225        *****************
'*****************        YONATTAN SERRANO TORRES 2014005692        *****************
'************************************************************************************

'' RESULTADOS:
''____________INSERTAR RESULTADOS DE LAS CORRIDAS

sampleAmount = 10000 '######## SAMPLE AMOUNT: CHANGE THIS VARIABLE TO GET PRECISION IN THE RESULT

'**********PROCEDURE: createDeck
newcmd createDeck deck
   copy 4#1,13 deck
   shuffle deck deck
   'print deck
end

'**********PROCEDURE: initGame
newcmd initGame playerCards dealerCards deck
   take deck (1 3) playerCards
   take deck (2 4) dealerCards
   remove deck (1 2 3 4) deck
  ' print playerCards dealerCards deck
end

'**********PROCEDURE: countCards
newcmd countCards cards total
   copy cards cardCopy
   recode cardCopy between 10 13 10 cardCopy
   sum cardCopy amountTotal
   if (1 memberof cardCopy) and (amountTotal + 10)<=21
      total = amountTotal + 10
   else
      total = amountTotal
   end
end

'**********PROCEDURE: getCard
newcmd getCard deck playerDeck
   take deck 1 card
   remove deck 1 deck
   copy playerDeck card playerDeck
end

'**********PROCEDURE: getWinner

newcmd getWinner playerCard dealerCard games
   countCards playerCard playerAmount
   countCards dealerCard dealerAmount
   print "------------Game-----------"
   print playerAmount
   print dealerAmount
   if playerAmount <= 21
      if dealerAmount<= 21
         if playerAmount > dealerAmount
            games = games +1 '' When the player win
            print "The player wins!!!!"
         else 
            print "The dealer wins!!!!"
         end
      else
         games = games + 1
         print "The player wins!!!!"
      end
    else 
        print "The dealer wins!!!!"
   end
end

'STRATEGIES ****************************************
'**********PROCEDURE: dealerStrategy
newcmd dealerStrategy cards flag visibleCard
   countCards cards amount
   if amount>=17
      flag = 0
   else
      flag = 1
   end
end

'**********PROCEDURE: playerSrategy
newcmd playerStrategy cards flag visibleCard
   countCards cards amount '**count the cards
   if (3<amount) and (amount<12)
      flag = 1
   elseif amount = 12
      if (3<visibleCard) and (visibleCard<7)
         flag = 0
      else
         flag = 1
      end
   elseif (12<amount) and (amount<17)
      if (6<visibleCard) or (visibleCard = 1)
         flag = 1
      else
         flag = 0
      end
   elseif amount>16
      flag = 0
   else 'default
      flag = 0
   end
end


'INDIVIDUALS GAMES ****************************************
'**********PROCEDURE: playerGame 
newcmd playerGame playerCards deck visibleCard
   flow = true
   while flow
      playerStrategy playerCards flag visibleCard
      if flag = 1
         getcard deck playerCards
      else
         flow = false
      end
   end
end
'**********PROCEDURE: dealerGame
newcmd dealerGame dealerCards deck visibleCard
   flow = true
   while flow
      dealerStrategy dealerCards flag visibleCard
      if flag = 1
         getcard deck dealerCards
      else
         flow = false
      end
   end
end
'**********PROCEDURE:game
newcmd game gamesAmount
   createDeck deck
   initGame playerCards dealerCards deck
'Get visible dealer card 
   take dealerCards 2 dealerCard
'Get visible player card
   take playerCards 2 playerCard
'init game
   playerGame playerCards deck dealerCard
   dealerGame dealerCards deck playerCard
' who is the winner ?
   getWinner playerCards dealerCards gamesAmount
end

'*********MAIN PROGRAM
gamesAmount = 0
copy sampleAmount flag
while flag>0
   game gamesAmount
   flag = flag - 1
end 
 
result = gamesAmount/sampleAmount
print "*******TEST RESULT*******"
print result

'···················· RESULTS ·························

' result: 0.4348
' result: 0.4379
' result: 0.4298
' result: 0.4205
' result: 0.4296
' result: 0.4339
' result: 0.4242
' result: 0.4315
' result: 0.4331
' result: 0.4258
' result: 0.4233
' result: 0.4269
' result: 0.4285
' result: 0.4331
' result: 0.4325
' result: 0.4269
' result: 0.4257
' result: 0.4285
' result: 0.4237
' result: 0.4272
'---------------
' Average: 0.42887


   
'******************************************************
' TEST AREA 
' Create deck
   
'createDeck deck
   
' Init game 
'initGame playerCards dealerCards deck
'copy (1 1 2) cards
'getCard deck cards
'countCards cards res
'print cards
